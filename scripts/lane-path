#!/usr/bin/env perl
#
# Author: pd3

use strict;
use warnings;
use Carp;
use VRTrack::VRTrack;
use VertRes::Utils::VRTrackFactory;
use VRTrack::Lane;

my $opts = parse_params();

our %db_to_root = (
    g1k_track => qq[$ENV{G1K}/TRACKING],
    g1k_meta  => qq[$ENV{G1K}/META],
    vrtrack_uk10k_cohort => qq[$ENV{UK10K}/TRACKING],
    vrtrack_uk10k_rare => qq[$ENV{UK10K}/TRACKING],
    vrtrack_uk10k_obesity => qq[$ENV{UK10K}/TRACKING],
    vrtrack_uk10k_neuro => qq[$ENV{UK10K}/TRACKING],
    mouse_reseq_track => qq[$ENV{MOUSE}/TRACKING],
);

our %dbs;
if (exists $$opts{db})
{
    error("db supplied ($$opts{db}) not known\n") unless (exists $db_to_root{$$opts{db}});
    $dbs{$$opts{db}} = $db_to_root{$$opts{db}};
}
else
{
    %dbs = %db_to_root;
}


for my $lane (@{$$opts{lanes}})
{
    my $paths = find_lane($lane);
    for my $path (@$paths)
    {
        print "$path\n";
    }
}
exit;


#--------------------------------

sub error
{
    my (@msg) = @_;
    if ( scalar @msg )
    {
        croak @msg;
    }
    die
        "Usage: lane-path [OPTIONS] <lane|run>\n",
        "Options:\n",
        "   -h, -?, --help                   This help message.\n",
        "\n";
}


sub parse_params
{
    my $opts = {};
    while (my $arg=shift(@ARGV))
    {
        if ( $arg eq '-?' || $arg eq '-h' || $arg eq '--help' ) { error(); }
        if ( $arg eq '--db' ) { $$opts{db}=shift(@ARGV); next; }
        push @{$$opts{lanes}}, $arg;
    }
    return $opts;
}



sub find_lane
{
    my ($lane) = @_;

    my $db_opts = { VertRes::Utils::VRTrackFactory->connection_details('rw') };

    my ($vrtrack,$db,@paths);
    for $db (keys %dbs)
    {
        $$db_opts{database} = $db;
        $vrtrack = VRTrack::VRTrack->new($db_opts);
        my $path = $vrtrack->hierarchy_path_of_lane_name($lane);
        if ( $path )
        {
            push @paths, "$dbs{$db}/$path";
            last;
        }
    }
    if ( scalar @paths ) { return \@paths; }

    # Try to find a run
    for $db (keys %dbs)
    {
        $$db_opts{database} = $db;
        $vrtrack = VRTrack::VRTrack->new($db_opts);
        
        my $query = "SELECT name FROM latest_lane WHERE name LIKE '${lane}\\_%'";
        my $sth = $vrtrack->{_dbh}->prepare($query);
        if ( !$sth ) { error("$query:\n", $!) }
        $sth->execute or error("$query:\n", $!);
        if ( !$sth->rows ) { next; }

        while (my @row=$sth->fetchrow_array)
        {
            my $path = $vrtrack->hierarchy_path_of_lane_name($row[0]);
            if ( $path )
            {
                push @paths, "$dbs{$db}/$path";
            }
        }
    }

    return \@paths;
}


