#!/usr/bin/env perl
#
# Author: petr.danecek@sanger
#

use strict;
use warnings;
use Carp;
use Utils;

my $runner = myRunner->new();
$runner->run();

exit;

#--------------------------------

package myRunner;
use base qw(Runner);
use strict;
use warnings;
use Vcf;

sub new
{
    my ($class,@args) = @_;
    my $self = $class->SUPER::new(@args);

    $$self{do_clean} = 1;
    $$self{debug_chunks} = 0;
    $$self{limits} = { memory=>5_000, runtime=>24*60 };
    $$self{bcftools}    = '/software/vertres/bin-external/bcftools-exp-rc';
    $$self{beagle_jar}  = '/nfs/users/nfs_p/pd3/sandbox/svn/beagle/b4.r1196.jar';
    $$self{beagle_args} = '';
    $$self{java_args} = '';
    $$self{phased_join_args} = '';
    $$self{memstep} = 2_000;
    $$self{config_version} = '1356084361';
    $$self{redo_beagle} = 0;
    $$self{_sampleconf} = q[
            # For sanity checking that your config file is not obsolete. Update the version key in your 
            #  config file to get rid of the warnings.
            version  => '] .$$self{config_version}. q[',

            beagle_jar  => '] .$$self{beagle_jar}. q[',
            beagle_args => '',
            java_args   => '',
            bcftools    => '] .$$self{bcftools}. q[',

            # The known_vcf (aka -k) and in_vcf (aka -i) file names can contain the string "{CHROM}" which will be expanded 
            #   according to chromosomes listed in the "region" key below. If the key is not defined, glob expansion will
            #   be used to obtain the chromosomes. The VCFs must be tabix indexed.
            # When known_vcf is set, consider also setting the chunk_by_known key.
            #
            # in_vcf     => '/some/path/chr{CHROM}.vcf.gz',
            # known_vcf  => '/nfs/users/nfs_p/pd3/sandbox/impute2/dec-chr20/Omni25_genotypes_1212_samples_v2.b37.vcf.gz',

            # If supplied, vcf check will be run to evaluate the performance
            # eval_vcf => undef,

            # Number of sites per chunk and in buffer regions. The total number of sites considered is buffer_nsites+chunk_nsites.
            #   The chunks are determined based on the input VCF (-i) unless chunk_by_known is set. 
            buffer_nsites  => 1_000,
            chunk_nsites   => 3_000,
            chunk_by_known => 0,

            # Indels usually have high false positives rate
            exclude_multiallelic_indels => 0,

            # If unset, imputed sites from known_vcf which are not present in in_vcf will not be printed on output. 
            #   This is useful when performing genotype refinement and the number of sites should stay unchanged.
            do_impute => 1,

            # Create whole-genome VCF? This can produce *huge* files for thousands of samples
            merge_chroms   => 0,

            # Regions to impute, the chunks must be sorted by chromosomal position or else the resulting VCF will not
            #   be sorted. By default, the whole VCF is imputed.
            # region => [ '1:159000000-161000000', '1:62000000-64000000', '2' ],

            limits   => { memory=>10_000, runtime=>24*60 },
            # How much memory should be given to Beagle in the next run if it fails because of out-of-memory error?
            #   Smaller step saves farm node's memory but can result in multiple runs (note that it can take
            #   considerable amount of CPU time before the memory is exhausted). On the other hand, bigger step may 
            #   block free CPUs on multi-core nodes by reserving too much memory.
            memstep  => '] .$$self{memstep}. q[',   # the step for increasing memory limit

            do_clean => 1,         # Remove runner's temporary files
    ]."\n";

    $$self{usage} .= 
        "Usage: run-beagle\n" .
        "Options:\n" .
        "   -i, --impute-vcf <file>             The target VCF file to be imputed or genotypes refined\n" .
        "   -m, --mrProper                      Clean all intermediate files, including Impute2 output files, leaving only top-level VCFs\n" .
        "   -o, --outdir <dir>                  Output directory\n" .
        "   -r, --region <chr|chr:from-to>      Run imputation in the given region only\n" .
        "\n";

    return $self;
}

sub parse_args
{
    my ($self) = @_;
    while (defined(my $arg=shift(@ARGV)))
    {
        if ( $arg eq '-m' or $arg eq '--mrProper' ) { $$self{mrProper}=1; next; }
        if ( $arg eq '-r' or $arg eq '--region' ) { $$self{region}=shift(@ARGV); next; }
        if ( $arg eq '-i' or $arg eq '--impute-vcf' ) { $$self{in_vcf}=shift(@ARGV); next; }
        if ( $arg eq '-k' or $arg eq '--known-vcf' ) { $$self{known_vcf}=shift(@ARGV); next; }
        if ( $arg eq '-o' or $arg eq '--outdir' ) { $$self{outdir}=shift(@ARGV); next; }
        $self->throw();
    }

    if ( !exists($$self{outdir}) ) { $self->throw("Missing the -o option.\n"); }
    if ( !exists($$self{in_vcf}) ) { $self->throw("Missing the -i option.\n"); }
    $$self{chunk_file} = "$$self{outdir}/chunks.txt";
    if ( $$self{mrProper} )
    {
        $self->mrProper($$self{outdir});
        $self->all_done;
    }
    if ( !exists($$self{version}) or $$self{version} ne $$self{config_version} )
    {
        $self->warn("Warning: Your config file may be out of date, the latest version key is \"$$self{config_version}\". Please run with +sampleconf to see what changed.\n");
    }
    if ( !exists($$self{known_vcf}) ) 
    { 
        $$self{known_vcf} = '--';
    }
    elsif ( !($$self{known_vcf}=~/.gz$/) )
    {
        $self->warn("The known_vcf should be bgzipped and tabix indexed. Hopefully you know what you're doing?\n");
    }
    if ( exists($$self{buffer_region}) or exists($$self{chunk_size}) )  
    { 
        $self->throw("The keys buffer_region and chunk_size are deprecated, please use buffer_nsites and chunk_nsites instead."); 
    }
    if ( !exists($$self{buffer_nsites}) ) 
    { 
        $self->throw("Missing the key buffer_nsites"); 
    }
    if ( !exists($$self{chunk_nsites}) ) 
    { 
         $self->throw("Missing the key chunk_nsites");
    }
    if ( exists($$self{chunk_by_known}) && !exists($$self{known_vcf}) )
    {
        $self->throw("Expected the key \"known_vcf\" with \"chunk_by_known\".\n");
    }
}

sub main
{
    my ($self) = @_;
    $self->parse_args();

    my $outdir = $$self{outdir};

    # Convert from VCFs to Beagle
    my $chunks = $self->read_chunks();
    for my $chunk (@$chunks)
    {
        $self->set_limits(%{$$self{limits}}) unless !exists($$self{limits});
        $self->spawn('beagle',"$outdir/$$chunk[0]/$$chunk[1]-$$chunk[2].done","$outdir/$$chunk[0]",$chunk);
    }
    $self->wait;

    # Convert to VCFs and concat
    if ( $$self{merge_chroms} ) 
    { 
        $self->set_limits(%{$$self{limits}},memory=>undef) unless !exists($$self{limits});
        $self->spawn('concat_vcfs',"$outdir/merged.vcf.gz",$chunks);
    }
    else
    {
        $self->set_limits(%{$$self{limits}},memory=>undef) unless !exists($$self{limits});
        my @chroms = $self->get_chroms($chunks);
        for my $chr (@chroms)
        {
            $self->spawn('concat_vcfs',"$outdir/$chr.vcf.gz",$chunks,$chr);
        }
    }
    $self->wait; 
    $self->set_limits(runtime=>undef);

    # Run performance stats and clean
    $self->spawn('runtime_stats',"$outdir/runtime");
    $self->wait;
    $self->clean($outdir) unless !$$self{do_clean};

    $self->all_done;
}

sub mrProper
{
    my ($self,$outdir) = @_;
    my $chunks = $self->read_chunks();
    my %chroms = ();
    for my $chunk (@$chunks) { $chroms{$$chunk[0]} = 1; }
    for my $chr (keys %chroms)
    {
        $self->cmd("rm -rf $outdir/$chr");
    }
    $self->SUPER::clean($outdir);
}

sub runtime_stats
{
    my ($self,$outfile) = @_;
    $self->cmd("mkdir -p $outfile.part");
    $self->cmd("runtime-stats $$self{outdir} -p $outfile.part/graph > $outfile.part/stats.txt");
    rename("$outfile.part",$outfile) or $self->throw("rename $outfile.part $outfile: $!");
}

sub read_chunks
{
    my ($self) = @_;
    if ( !-e $$self{chunk_file} ) 
    { 
        my $in_file = $$self{chunk_by_known} ? $$self{known_vcf} : $$self{in_vcf};
        my @regions = $self->define_regions($in_file);
        for my $region (@regions)
        {
            $self->spawn('define_chunks',"$$self{chunk_file}.$region",$in_file,$region);
        }
        $self->wait;

        $self->cmd("> $$self{chunk_file}.part");
        for my $region (@regions)
        {
            $self->cmd("cat $$self{chunk_file}.$region >> $$self{chunk_file}.part");
            $self->spawn('define_chunks',"$$self{chunk_file}.$region",$in_file,$region);
        }
        rename("$$self{chunk_file}.part",$$self{chunk_file}) or $self->throw("rename $$self{chunk_file}.part $$self{chunk_file}: $!");
        for my $region (@regions)
        {
            unlink("$$self{chunk_file}.$region");
        }
    }
    open(my $fh,'<',$$self{chunk_file}) or $self->throw("$$self{chunk_file}: $!");
    my @chunks;
    while (my $line=<$fh>)
    {
        chomp($line);
        my @items = split(/\t/,$line);
        push @chunks, [@items];
    }
    close($fh) or $self->throw("$$self{chunk_file}: $!");
    return \@chunks;
}

sub define_chunks
{
    my ($self,$outfile,$in_file,$region) = @_;
    my $tot_sites = $$self{buffer_nsites} + $$self{chunk_nsites};
    my (@chunks,@buffer);
    my $file = $self->expand_chrom($in_file,$region);
    my $cmd  = "tabix $file $region |";
    open(my $in,$cmd) or $self->throw("$cmd: $!");
    while (my $line=<$in>)
    {
        if ( substr($line,0,1) eq '#' ) { next; }
        my $i = index($line,"\t");
        if ( $i<0 ) { $self->throw("Could not parse the line [CHR]: $line"); }
        my $chr = substr($line,0,$i);
        my $j = index($line,"\t",$i+1);
        if ( $j<0 ) { $self->throw("Could not parse the line [POS]: $line"); }
        my $pos = substr($line,$i+1,$j-$i-1);

        if ( @buffer && $buffer[0][0] ne $chr or @buffer>$tot_sites )
        {
            my $chr_from = $buffer[0][0];
            my $pos_from = $buffer[0][1];
            my $pos_to   = $buffer[-1][1];
            my $nout     = @buffer;
            push @chunks, { chr=>$chr_from, from=>$pos_from, to=>$pos_to, n=>$nout };
            if ( $$self{chunk_nsites}<@buffer ) { splice(@buffer,0,$$self{chunk_nsites}); }
            else { @buffer = (); }
        }
        push @buffer, [$chr,$pos];
    }
    if ( @buffer )
    {
        my $chr_from = $buffer[0][0];
        my $pos_from = $buffer[0][1];
        my $pos_to   = $buffer[-1][1];
        my $nout     = @buffer;
        push @chunks, { chr=>$chr_from, from=>$pos_from, to=>$pos_to, n=>$nout };
    }
    close($in) or $self->throw("close $cmd");

    if ( !@chunks ) { $self->throw("No chunks defined?\n"); }
    if ( @chunks>1 && $chunks[-1]{n} < $tot_sites*0.75 )
    {
        my $chunk = splice(@chunks,-1,1);
        $chunks[-1]{to} = $$chunk{to};
        $chunks[-1]{n} += $$chunk{n};
    }

    open(my $out,'>',"$outfile.part") or $self->throw("$outfile.part: $!");
    for my $chunk (@chunks)
    {
        print $out "$$chunk{chr}\t$$chunk{from}\t$$chunk{to}\t$$chunk{n}\n";
    }
    close($out) or $self->throw("close $outfile.part");
    rename("$outfile.part",$outfile);
}

sub get_chroms
{
    my ($self,$regions) = @_;
    my %chroms;
    for my $reg (@$regions) 
    { 
        $chroms{$$reg[0]} = 1; 
    }
    return ( sort keys %chroms );
}

sub expand_chrom
{
    my ($self,$path,$region) = @_;
    $region =~ s/:.*$//;
    $path =~ s/{CHROM}/$region/g;
    return $path;
}

sub define_regions
{
    my ($self,$path) = @_;
    if ( exists($$self{region}) ) { return @{$$self{region}}; }
    if ( !($path=~/{CHROM}/) ) { return ('.'); }
    my $before = $`;
    my $after  = $';
    $path =~ s/{CHROM}/*/;  # only one occurance of {CHROM} is allowed in this implementation
    my @list = glob($path);
    my @regs;
    for my $file (@list)
    {
        $file =~ s{^$before}{};
        $file =~ s{$after$}{};
        push @regs,$file;
    }
    return @regs;
}

sub next_beagle_rec
{
    my ($self,$vcf,$rec, $out_fh,$out_vcf) = @_;
    while (my $x = $vcf->next_data_array)
    {
        if ( $$x[0] ne $$rec[0] ) { $self->throw("Different chromosomes?!  $$x[0] vs $$rec[0]\n"); }
        if ( $$x[1] < $$rec[1] )
        {
            # Out of sync: The beagle site not present in the original VCF
            if ( !$$self{known_vcf} ) { $self->throw("Out of sync? $$x[0]:$$x[1] vs $$rec[0]:$$rec[1]\n"); }
            if ( $$self{do_impute} ) { print $out_fh $out_vcf->format_line($x); }
            next;
        }
        if ( $$x[1] > $$rec[1] ) { $self->throw("Out of sync? $$x[0]:$$x[1] vs $$rec[0]:$$rec[1]\n"); }
        return $x;
    }
    return undef;
}

sub merge_beagle_vcf 
{
    my ($self,$chr,$from,$to,$ori_fname,$new_fname,$out_fname) = @_;
    my $region  =  "$chr:$from-$to";
    my $ori_vcf = Vcf->new(file=>$ori_fname, region=>$region);
    my $new_vcf = Vcf->new(file=>$new_fname);
    $ori_vcf->parse_header();
    $new_vcf->parse_header();
    my @ori_samples = $ori_vcf->get_samples();
    my @new_samples = $new_vcf->get_samples();
    if ( scalar @ori_samples != scalar @new_samples ) { $self->throw(sprintf "Different number of samples: %d vs %d\n", scalar @ori_samples,scalar @new_samples); }
    for (my $i=0; $i<@ori_samples; $i++)
    {
        if ( $ori_samples[$i] ne $new_samples[$i] ) { $self->throw("Different sample ordering? $i ..  $ori_samples[$i] vs $new_samples[$i]\n"); }
    }
    my $nsamples = @ori_samples;
    open(my $fh,"| bgzip -c > $out_fname") or $self->throw("| bgzip -c > $out_fname: $!"); 
    $ori_vcf->add_header_line({key=>'INFO', ID=>'AC',Number=>'A',Type=>'Integer',Description=>'Allele count in genotypes'});
    $ori_vcf->add_header_line({key=>'INFO', ID=>'AN',Number=>'1',Type=>'Integer',Description=>'Total number of alleles in called genotypes'});
    $ori_vcf->add_header_line({key=>'FORMAT', ID=>'GL',Number=>'G',Type=>'Float',Description=>'Estimated Genotype Likelihood (Beagle)'});
    $ori_vcf->add_header_line({key=>'FORMAT', ID=>'DS',Number=>'1',Type=>'Float',Description=>'Estimated ALT dose [P(RA) + P(AA)] (Beagle)'});
    print $fh $ori_vcf->format_header();
    my $prev;
    while (my $ori_rec = $ori_vcf->next_data_array())
    {
        if ( $$ori_rec[1] < $from ) { next; }
        if ( $$self{exclude_multiallelic_indels} && $self->is_multiallelic_indel($$ori_rec[3],$$ori_rec[4]) ) { print $fh $ori_vcf->format_line($ori_rec); next; }

        # skip duplicate positions
        if ( defined $prev && $prev eq $$ori_rec[1] ) 
        { 
            print $fh $ori_vcf->format_line($ori_rec);
            next; 
        }
        $prev = $$ori_rec[1];

        my $new_rec = $self->next_beagle_rec($new_vcf, $ori_rec, $fh, $ori_vcf);
        if ( !defined $new_rec ) { $self->throw("No more records from $new_fname? $$ori_rec[0]:$$ori_rec[1]\n"); }
        if ( $$new_rec[0] ne $$ori_rec[0] ) { $self->throw("CHR mismatch at $$ori_rec[0]:$$ori_rec[1] .. $$new_rec[3] vs $$ori_rec[3]\n"); }
        if ( $$new_rec[1] ne $$ori_rec[1] ) { $self->throw("POS mismatch at $$ori_rec[0]:$$ori_rec[1] .. $$new_rec[3] vs $$ori_rec[3]\n"); }
        if ( $$new_rec[3] ne $$ori_rec[3] ) { $self->throw("REF mismatch at $$ori_rec[0]:$$ori_rec[1] .. $$new_rec[3] vs $$ori_rec[3]\n"); }
        if ( $$new_rec[4] ne $$ori_rec[4] ) { $self->throw("ALT mismatch at $$ori_rec[0]:$$ori_rec[1] .. $$new_rec[4] vs $$ori_rec[4]\n"); }
        if ( $$new_rec[8] ne 'GT:DS:GP' ) { $self->throw("Update me, the FORMAT field at $$ori_rec[0]:$$ori_rec[1] changed to $$new_rec[8]\n"); }
        my @alt = split(/,/,$$ori_rec[4]);
        my $nal = 1 + scalar @alt;
        my $igt = $ori_vcf->get_tag_index($$ori_rec[8],'GT',':');
        my $ipl = $ori_vcf->get_tag_index($$ori_rec[8],'PL',':');
        my $igp = $ori_vcf->get_tag_index($$ori_rec[8],'GP',':');
        my $igl = $ori_vcf->get_tag_index($$ori_rec[8],'GL',':');
        my $ids = $ori_vcf->get_tag_index($$ori_rec[8],'DS',':');
        if ( $ids!=-1 ) { $self->throw("Update me, FORMAT/DS already exist in $ori_fname"); }
        if ( $igp!=-1 ) { $self->throw("Update me, FORMAT/GP already exist in $ori_fname"); }
        if ( 0 != $new_vcf->get_tag_index($$new_rec[8],'GT',':') ) { $self->throw("Expected GT:DS:GP, got $$new_rec[8] at $$new_rec[0]:$$new_rec[1]\n"); }
        if ( 1 != ($ids=$new_vcf->get_tag_index($$new_rec[8],'DS',':')) ) { $self->throw("Expected GT:DS:GP, got $$new_rec[8] at $$new_rec[0]:$$new_rec[1]\n"); }
        if ( 2 != ($igp=$new_vcf->get_tag_index($$new_rec[8],'GP',':')) ) { $self->throw("Expected GT:DS:GP, got $$new_rec[8] at $$new_rec[0]:$$new_rec[1]\n"); }
        $$ori_rec[8] .= ':DS';
        if ( $igl<0 ) { $$ori_rec[8] .= ':GL'; }
        my @ac = ();
        for (my $i=0; $i<$nsamples; $i++)
        {
            my $icol = $i+9;
            my $gt = $ori_vcf->get_field($$ori_rec[$icol],$igt);
            my $ds = $new_vcf->get_field($$new_rec[$icol],$ids);
            if ( index($gt,'/')==-1 && index($gt,'|')==-1 )
            {
                # haploid
                my $new_gt = $new_vcf->get_field($$new_rec[$icol],0);
                $new_gt = substr($new_gt,0,1);
                $ac[$new_gt]++;

                my @gp = split(/,/, $new_vcf->get_field($$new_rec[$icol],$igp));
                my @new_gp;
                my $j = 0;
                my $k = 1;
                while ($j<@gp)
                {
                    my $val = $gp[$j]!=0 ? log($gp[$j])/2.302585 : -99;
                    if ( $val < -99 ) { $val = -99; }
                    push @new_gp, $val;
                    $j += ++$k;
                }

                my @ori_pl = split(/,/,$ori_vcf->get_field($$ori_rec[$icol], $ipl));
                my @new_pl;
                for ($j=0; $j<$nal; $j++)
                {
                    push @new_pl, $ori_pl[$j];
                }
                $$ori_rec[$icol] = $ori_vcf->replace_field($$ori_rec[$icol],$new_gt,$igt,':');
                $$ori_rec[$icol] = $ori_vcf->replace_field($$ori_rec[$icol],join(',',@new_pl),$ipl,':');
                $$ori_rec[$icol] .= ':' . $ds;
                if ( $igl<0 ) { $$ori_rec[$icol] .= ':' . join(',',@new_gp); }
                else { $ori_vcf->replace_field($$ori_rec[$icol],join(',',@new_gp),$igl,':'); }
            }
            else
            {
                # diploid
                my $new_gt = $new_vcf->get_field($$new_rec[$icol],0);
                my @gt = $new_vcf->split_gt($new_gt);
                if ( scalar @gt !=2 ) { $self->throw("Uh: $new_gt"); }
                for my $gt (@gt) { $ac[$gt]++; }

                my @new_gp = split(/,/, $new_vcf->get_field($$new_rec[$icol],$igp));
                for (my $j=0; $j<@new_gp; $j++)
                {
                    my $val = $new_gp[$j]!=0 ? log($new_gp[$j])/2.302585 : -99;
                    if ( $val < -99 ) { $val = -99; }
                    $new_gp[$j] = $val;
                }
                $$ori_rec[$icol] = $ori_vcf->replace_field($$ori_rec[$icol],$new_gt,0,':');
                $$ori_rec[$icol] .= ':' . $ds;
                if ( $igl<0 ) { $$ori_rec[$icol] .= ':' . join(',',@new_gp); }
                else { $ori_vcf->replace_field($$ori_rec[$icol],join(',',@new_gp),$igl,':'); }
            }
        }
        my $an = 0;
        for my $ac (@ac) { $an += $ac ? $ac : 0; }
        shift(@ac);
        my $ac = scalar @ac ? join(',',@ac) : undef;
        $$ori_rec[7] = $ori_vcf->add_info_field($$ori_rec[7],'AC'=>$ac,'AN'=>$an);
        print $fh $ori_vcf->format_line($ori_rec);
    }
    close($fh);
}

sub is_multiallelic_indel
{
    my ($self,$ref,$alt) = @_;
    my $i = index($alt,',');
    if ( $i==-1 ) { return 0; }
    if ( length($ref)>1 or $i>1 ) { return 1; }
    return 0;
}

sub haploid_to_diploid
{
    my ($self,$vcf,$rec) = @_;

    my $igt = $vcf->get_tag_index($$rec[8],'GT',':');
    my $ipl = $vcf->get_tag_index($$rec[8],'PL',':');
    if ( $igt==-1 ) { $self->throw("GT not present in $$vcf{file} at $$rec[0]:$$rec[1]\n"); }
    if ( $ipl==-1 ) { $self->throw("PL not present in $$vcf{file} at $$rec[0]:$$rec[1]\n"); }
    my @alt = split(/,/,$$rec[4]);
    my $nal = 1 + scalar @alt;
    my $ncols = @{$$vcf{columns}};
    for (my $i=9; $i<$ncols; $i++)
    {
        my $gt = $vcf->get_field($$rec[$i], $igt);
        if ( index($gt,'/')>=0 || index($gt,'|')>=0 ) { next; }    # diploid
        my $new_gt = "$gt/$gt";

        my $pl  = $vcf->get_field($$rec[$i], $ipl);
        my @pls = split(/,/, $pl);

        if ( @pls!=$nal ) { $self->throw("Wrong number of PL fields? col=$i, nal=$nal, $pl=$pl: ", join("\n",@$rec)); }

        my @new_pl;
        for (my $j=0; $j<$nal; $j++)
        {
            for (my $k=0; $k<$j; $k++) { push @new_pl, 999; }  # set likelihoods of hets to zero
            push @new_pl, $pls[$j];
        }
        $$rec[$i] = $vcf->replace_field($$rec[$i], $new_gt, $igt, ':');
        $$rec[$i] = $vcf->replace_field($$rec[$i], join(',',@new_pl), $ipl, ':');
    }
}

sub create_vcf_chunk
{
    my ($self,$in_vcf,$out_vcf,$chr,$from,$to) = @_;
    my $vcf = Vcf->new(file=>$in_vcf, region=>"$chr:$from-$to");
    $vcf->parse_header();

    open(my $out,"| gzip -c > $out_vcf") or $self->throw("gzip -c > $out_vcf: $!");
    print $out $vcf->format_header();

    my $prev;
    while (my $rec = $vcf->next_data_array())
    {
        if ( $$rec[1] < $from ) { next; }
        if ( $$self{exclude_multiallelic_indels} && $self->is_multiallelic_indel($$rec[3],$$rec[4]) ) { next; }

        # skip duplicate positions
        if ( defined $prev && $prev eq $$rec[1] ) { next; }
        $prev = $$rec[1];

        $self->haploid_to_diploid($vcf, $rec);
        print $out $vcf->format_line($rec);
    }
    $vcf->close();
    close($out) or error("close gzip -c > $out_vcf");
}

sub beagle
{
    my ($self,$outfile,$outdir,$chunk) = @_;
    my $prefix = "$outdir/$$chunk[1]-$$chunk[2]";
    my $chr  = $$chunk[0];
    my $from = $$chunk[1];
    my $to   = $$chunk[2];
    my $in_vcf    = $self->expand_chrom($$self{in_vcf},$chr);
    my $known_vcf = exists($$self{known_vcf}) ? $self->expand_chrom($$self{known_vcf},$chr) : undef;

    if ( !-e "$prefix.vcf.gz" or $$self{redo_beagle} )
    {
        $self->cmd(qq[mkdir -p $outdir]);
        my $mem = $self->get_limits('memory') * 0.8;
        if ( $mem<=0 ) { $mem = 500; }
        my $known = defined $known_vcf ? '' : "ref=$known_vcf";
        $self->create_vcf_chunk($in_vcf,"$prefix.in.vcf.gz",$chr,$from,$to);
        $self->java_cmd(qq[java -Xms${mem}m -Xmx${mem}m $$self{java_args} -jar $$self{beagle_jar} $$self{beagle_args} $known gl=$prefix.in.vcf.gz out=$prefix.part >>$outfile.o],"$outfile.e");
        if ( -s "$outfile.e" ) { $self->throw("Expected empty error file: $outfile.e"); }
        rename("$prefix.part.vcf.gz","$prefix.vcf.gz") or $self->throw("rename $prefix.part.vcf.gz $prefix.vcf.gz: $!");
        unlink("$prefix.in.vcf.gz");
    }
    $self->merge_beagle_vcf($chr,$from,$to,$in_vcf,"$prefix.vcf.gz","$outdir/$$chunk[1]-$$chunk[2].vcf.gz.part");
    $self->tabix_part("$outdir/$$chunk[1]-$$chunk[2].vcf.gz");
    #rename("$prefix.vcf.gz","$prefix.vcf.gz-beagle");  # uncomment to save raw beagle output
    unlink("$prefix.vcf.gz");
    rename("$outdir/$$chunk[1]-$$chunk[2].vcf.gz.part","$outdir/$$chunk[1]-$$chunk[2].vcf.gz");
    $self->cmd(qq[touch $outfile]);
}

sub concat_vcfs
{
    my ($self,$outfile,$chunks,$chr) = @_;
    open(my $fh,'>',"$outfile.list") or $self->throw("$outfile.list: $!");
    for my $chunk (@$chunks)
    {
        if ( $$chunk[0] ne $chr ) { next; }
        my $file = "$$self{outdir}/$$chunk[0]/$$chunk[1]-$$chunk[2].vcf.gz";
        print $fh $file, "\n";
    }
    close($fh);
    $self->cmd(qq[vcf-phased-join $$self{phased_join_args} -l $outfile.list -o - 2>>$outfile.log | bgzip -c > $outfile.part]);
    $self->tabix_part($outfile);

    if ( !defined($$self{eval_vcf}) )
    {
        rename("$outfile.part",$outfile);
        return;
    }

    rename("$outfile.part","$outfile.tmp.vcf.gz");
    rename("$outfile.tbi","$outfile.tmp.vcf.gz.tbi");
    my $args = defined $chr ? "-r $chr" : '';
    $self->cmd(qq[$$self{bcftools} stats $args -s - $$self{eval_vcf} $outfile.tmp.vcf.gz > $outfile.vchk]);
    rename("$outfile.tmp.vcf.gz.tbi","$outfile.tbi");
    rename("$outfile.tmp.vcf.gz","$outfile");
}

sub tabix_part
{
    my ($self,$vcf) = @_;
    $self->cmd("tabix -p vcf -f $vcf.part");
    rename("$vcf.part.tbi","$vcf.tbi");
}

sub cmd
{
    my ($self,$cmd) = @_;
    return Utils::CMD($cmd,{verbose=>1});
}
sub java_cmd
{
    my ($self,$cmd,$err_file) = @_;

    print STDERR "$cmd 2>$err_file\n";
    if ( -e $err_file ) { `cat $err_file >> $err_file.prev`; unlink($err_file); }

    `$cmd 2>$err_file`;
    if ( ! $? ) { return; } # finished OK

    # something went wrong
    my $out_of_memory = 0;
    open(my $fh,'<',$err_file) or $self->throw("$err_file: $!");
    while (my $line=<$fh>)
    {
        if ( $line=~/java.lang.OutOfMemoryError/ ) { $out_of_memory = 1; last; }
        if ( $line=~/Could not create the Java virtual machine/ ) { $out_of_memory = 1; last; }
    }
    close($fh);
    if ( !$out_of_memory ) { $self->throw("The command failed: $cmd\n"); }

    # out of memory: tell the runner to use more memory next time
    my $mem = $self->get_limits('memory') + $$self{memstep};
    $self->set_limits(memory=>$mem);

    $self->throw("Memory limit exceeded, increasing the memory limit for the next run to $mem. The command was:\n$cmd\n");
}


